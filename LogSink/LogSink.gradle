// Plugins used for LogSink
plugins {
    // using the Java core Application plugin
    id 'application'
    // JIB Container Support for Java applications
    id 'com.google.cloud.tools.jib' version '3.1.2'
}

mainClassName = 'de.fraunhofer.iosb.ivct.logsink.LogSink'

jib {
    from {
        image = 'openjdk:8-jre-alpine'
    }
    to {
        image = "ivct/logsink:${ivctVersion}"
    }
	container {
		creationTime = 'USE_CURRENT_TIMESTAMP'
	}
}

tasks.register ("dockerBuildImage") {
    group = 'Docker'
    description = 'Creates docker image for LogSink'
    dependsOn 'jibDockerBuild'
}

dependencies {
	// local project dependencies
	implementation  project(':Command')
	// external project dependencies
	implementation 	libraries.actimemqClient
	implementation  libraries.slf4j_api
	implementation	libraries.jsonSimple
	implementation	libraries.jsonIo
    implementation	libraries.logback_core
    implementation	libraries.logback_classic
    implementation  libraries.logback_contrib
    implementation  libraries.logback_json_classic
}

distributions {
    main {
        contents {
            from('src/conf/logback.xml') {
                into 'lib'
            }
        }
    }
}

tasks.withType(CreateStartScripts) {
    classpath += files('lib')
doLast {
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile = file getUnixScript()
        windowsScriptFile.text = windowsScriptFile.text.replace('%APP_HOME%\\lib\\lib', '%APP_HOME%\\lib')
        unixScriptFile.text = unixScriptFile.text.replace('$APP_HOME/lib/lib', '$APP_HOME/lib')
    }}
