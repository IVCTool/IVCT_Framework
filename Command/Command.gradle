plugins {
    id 'java-library'
}


task writeVersion {
    group = "Build"
    description = "Creates a properties file with version information"
    doLast {
        def prop = new Properties()
        prop.version = ivctVersion
        prop.build = "git describe --tags".execute().text.trim()
        mkdir "src/main/resources"
        File propFile = file('src/main/resources/dev.properties')
        prop.store(propFile.newWriter(), null)
    }
}

processResources.dependsOn writeVersion

dependencies {
	// local project dependencies
	implementation	project(':MessagingHelpers')
	// external project dependencies
	api	libraries.slf4j_api
	api	libraries.jsonSimple
	implementation libraries.apacheCommonsIO
    // for testing with embedded broker    
	testImplementation libraries.junit_jupiter_engine
	testImplementation libraries.activemqEmbedded
}

test {
    // Enable JUnit 5 (Gradle 4.6+).
    useJUnitPlatform()
}

javadoc {
    options.addStringOption('XDoclint:none', '-quiet')
    failOnError = false
}
