apply plugin: 'application'

mainClassName = 'de.fraunhofer.iosb.ivct.CmdLineTool'

dependencies {
	implementation     project(':Command')
    implementation     project(':MessagingHelpers')
	implementation     libraries.slf4j_api
	implementation     libraries.jsonSimple


	implementation		libraries.actimemqClient
	implementation		libraries.logback_classic
	implementation		libraries.logback_core

	// testImplementation	libraries.junit
	// testImplementation	libraries.hamcrest_core
	testImplementation	libraries.junit_jupiter_engine
	testImplementation	libraries.activemqEmbedded
}

distributions {
    main {
        contents {
            from('src/conf/CmdLineTool_logback.xml') {
                into 'lib'
            }
        }
    }
}

sourceSets {
  test {
    resources {
      srcDir "src/conf"
    }
  }
}

test {
	classpath = sourceSets.test.runtimeClasspath + files('src/conf')
    // Enable JUnit 5 (Gradle 4.6+).
    useJUnitPlatform()
}

tasks.withType(CreateStartScripts) {
    classpath += files('lib')
doLast {
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile = file getUnixScript()
        windowsScriptFile.text = windowsScriptFile.text.replace('%APP_HOME%\\lib\\lib', '%APP_HOME%\\lib')
        unixScriptFile.text = unixScriptFile.text.replace('$APP_HOME/lib/lib', '$APP_HOME/lib')
    }
}
