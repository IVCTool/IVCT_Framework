apply plugin: 'application'

mainClassName = 'de.fraunhofer.iosb.testrunner.JMSTestRunner'

dependencies {
	compile     project(':Command')
	compile     project(':TC.lib')

	compile     libraries.slf4j_api
	compile     libraries.jsonSimple

	runtime		libraries.actimemqClient
	runtime		libraries.logback_classic
	runtime		libraries.logback_core

	runtime     libraries.slf4j_jcl_over_slf4j
	runtime     libraries.slf4j_jul_to_slf4j
	runtime     libraries.slf4j_log4j_over_slf4j

    switch (rti) {
		case "prti1516e":
			compileOnly files (rti_lib + "/" + rti_jar)
			compileOnly fileTree (dir: rti_lib, include: '*.jar')
			break
		case "mak":
			logger.warn (':TC:MAK RTI is not configured. HelloWorld will not be runnable')
			compileOnly     group: 'msg134-ivct-framework', name: 'IEEE1516e', version: ivctVersion
			break
		case "IEEE1516e":
		default:
			logger.warn (':TC:No RTI is used. Test Engine will not be able to run test cases')
			compileOnly     group: 'msg134-ivct-framework', name: 'IEEE1516e', version: ivctVersion
	}
}
distributions {
    main {
        contents {
            from('src/conf/TC.execPitch.bat') {
                into 'bin'
            }
            from('src/conf/TC.execPitch') {
                into 'bin'
            }
            from('src/conf/TC.execMAK.bat') {
                into 'bin'
            }
            from('src/conf/TC.execMAK') {
                into 'bin'
            }
            from('src/conf/JMSTestRunner_logback.xml') {
                into 'lib'
            }
        }
    }
}

tasks.withType(CreateStartScripts) {
	/*
	 * Want to have the lib folder (for the logback xml file) included in classpath,
	 * but gradle makes lib/lib out of that.
	 * Thus need to edit the script to only have a single lib level.
	 * A second piece of logic is that separate scripts are made for the various RTIs
	 * using the standard environment variables for each RTI. A script is needed for
	 * each of UNIX and Windows for each RTI.
	 */
    classpath += files('lib')
doLast {
		/*
		* Get the default scripts from the gradle task.
		*/
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile = file getUnixScript()
		def myName = project.name

		/*
		 * Prepare the various script file names.
		 */
		def file1 = new File(myName, '/src/conf/TC.execPitch.bat')
		def file2 = new File(myName, '/src/conf/TC.execMAK.bat')
		def file3 = new File(myName, '/src/conf/TC.execPitch')
		def file4 = new File(myName, '/src/conf/TC.execMAK')

		/*
		 * Produce the script files by editing the default files by locating the %APP_HOME%/lib/lib string.
		 * At that point modify the string to %APP_HOME%/lib for the logback file and add the
		 * necessary jar files for MAK or Pitch from the external folders located by the standard
		 * environment variables for each RTI respectively.
		 */
		file1.text = windowsScriptFile.text.replace('%APP_HOME%\\lib\\lib', '%APP_HOME%\\lib;%PRTI1516E_HOME%\\lib\\prti1516e.jar')
		file2.text = windowsScriptFile.text.replace('%APP_HOME%\\lib\\lib', '%APP_HOME%\\lib;%MAK_RTIDIR%\\lib\\java\\*')
		file3.text = unixScriptFile.text.replace('$APP_HOME/lib/lib', '$APP_HOME/lib/$PRTI1516E_HOME/lib/prti1516e.jar')
		file4.text = unixScriptFile.text.replace('$APP_HOME/lib/lib', '$APP_HOME/lib/$MAK_RTIDIR/lib/java/*')

		/*
		 * These files are not needed, but fix the lib/lib to lib anyway for consistency. Maybe these files
		 * can deleted at the end of the build.
		 */
		windowsScriptFile.text = windowsScriptFile.text.replace('%APP_HOME%\\lib\\lib', '%APP_HOME%\\lib')
		unixScriptFile.text = unixScriptFile.text.replace('$APP_HOME/lib/lib', '$APP_HOME/lib')
    }
}
